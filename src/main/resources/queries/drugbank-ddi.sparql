prefix np: <http://www.nanopub.org/nschema#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sio: <http://semanticscience.org/resource/>
prefix has-source: <http://semanticscience.org/resource/SIO_000253>
prefix void: <http://rdfs.org/ns/void#>
prefix pav: <http://purl.org/pav/2.0/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix dv: <http://bio2rdf.org/drugbank_vocabulary:>

insert {
  graph ?headGraph {
    ?nanopub a np:Nanopublication .
    ?nanopub np:hasAssertion ?assertionGraph .
    ?nanopub np:hasProvenance ?provenanceGraph .
    ?nanopub np:hasPublicationInfo ?publicationInfoGraph .
  }
  graph ?assertionGraph	{
    ?ddi a dv:Drug-Drug-Interaction .
    ?ddi rdfs:label ?label .
    ?ddi dcterms:title ?title .
    ?ddi dcterms:identifier ?identifier .
    ?drug dv:ddi-interactor-in ?ddi .
  }
  graph ?provenanceGraph {
    ?assertionGraph has-source: ?dataset .
  }
  graph ?publicationInfoGraph {
    # TODO: author/creator?
    ?nanopub dcterms:created ?now .
    # TODO: license and rights holder?
  }
} where {
  service <http://localhost:8899/sparql> {
    select distinct * {
      ?ddi a dv:Drug-Drug-Interaction .
      ?ddi rdfs:label ?label .
      ?ddi dcterms:title ?title .
      ?ddi dcterms:identifier ?identifier .
      ?ddi void:inDataset ?dataset .
      ?drug dv:ddi-interactor-in ?ddi .
    }
  }
  bind(iri(concat(str(?ddi),"_nanopub")) as ?nanopub)
  bind(iri(concat(str(?nanopub),"head")) as ?headGraph)
  bind(iri(concat(str(?nanopub),"assertion")) as ?assertionGraph)
  bind(iri(concat(str(?nanopub),"provenance")) as ?provenanceGraph)
  bind(iri(concat(str(?nanopub),"publicationInfo")) as ?publicationInfoGraph)
  bind(now() as ?now)
}
