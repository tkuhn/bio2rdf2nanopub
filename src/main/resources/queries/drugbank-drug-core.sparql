prefix np: <http://www.nanopub.org/nschema#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix sio: <http://semanticscience.org/resource/>
prefix void: <http://rdfs.org/ns/void#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix dctypes: <http://purl.org/dc/dcmitype/>
prefix v: <http://bio2rdf.org/bio2rdf_vocabulary:>
prefix d: <http://bio2rdf.org/drugbank:>
prefix dv: <http://bio2rdf.org/drugbank_vocabulary:>
prefix dr: <http://bio2rdf.org/drugbank_resource:>
prefix dcat: <http://www.w3.org/ns/dcat#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix pav: <http://purl.org/pav/>

insert {
  graph ?headGraph {
    ?nanopub a np:Nanopublication .
    ?nanopub np:hasAssertion ?assertionGraph .
    ?nanopub np:hasProvenance ?provenanceGraph .
    ?nanopub np:hasPublicationInfo ?publicationInfoGraph .
  }
  graph ?assertionGraph	{
    ?drug a dv:Drug .
    ?drug v:identifier ?id .
    ?drug v:namespace ?ns .
    ?drug v:uri ?uri .
    ?drug rdfs:label ?label .
    ?drug rdfs:seeAlso ?seealso .
    ?drug dcterms:title ?title .
    ?drug dcterms:description ?desc .
    ?drug dcterms:identifier ?identifier .
    ?drug dv:drugbank-id ?drugbankid .
    ?drug dv:category ?category .
    ?drug v:x-identifiers.org ?xidentifiersorg .
    ?drug dv:x-ahfs ?xahfs .
    ?drug dv:x-atc ?xatc .
    ?drug dv:x-bindingdb ?xbindingdb .
    ?drug dv:x-cas ?xcas .
    ?drug dv:x-chebi ?xchebi .
    ?drug dv:x-chemspider ?xchemspider .
    ?drug dv:x-dpd ?xdpd .
    ?drug dv:x-genbank ?xgenbank .
    ?drug dv:x-gtp ?xgtp .
    ?drug dv:x-iuphar ?xiuphar .
    ?drug dv:x-kegg ?xkegg .
    ?drug dv:x-ndc ?xndc .
    ?drug dv:x-pdb ?xpdb .
    ?drug dv:x-pharmgkb ?xpharmgkb .
    ?drug dv:x-pubchemcompound ?xpubchemcompound .
    ?drug dv:x-pubchemsubstance ?xpubchemsubstance .
    ?drug dv:x-uniprot ?xuniprot .
    ?drug dv:x-wikipedia ?xwikipedia .
  }
  graph ?provenanceGraph {
    ?assertionGraph prov:wasDerivedFrom ?distribution .
    ?dataset dcat:distribution ?distribution .
    ?distribution dcterms:created ?distributionCreated .
    ?distribution prov:wasDerivedFrom ?distributionRetrievedSource .
    ?distributionRetrievedSource prov:wasDerivedFrom ?distributionSource .
    ?distributionRetrievedSource pav:retrievedOn ?distributionSourceRetrievedOn .
    ?distributionSource prov:wasAttributedTo ?distributionSourcePublisher .
  }
  graph ?publicationInfoGraph {
    ?nanopub dcterms:created ?now .  # This triple is replaced later
    # TODO: license and rights holder?
  }
} where {
  service <http://localhost:8899/sparql> {
    select distinct * {
      ?drug a dv:Drug .
      ?drug rdfs:label ?label .
      ?drug v:identifier ?id .
      ?drug v:namespace ?ns .
      ?drug v:uri ?uri .
      ?drug rdfs:seeAlso ?seealso .
      ?drug dcterms:title ?title .
      optional { ?drug dcterms:description ?desc . }
      ?drug dcterms:identifier ?identifier .
      ?drug dv:drugbank-id ?drugbankid .
      ?drug v:x-identifiers.org ?xidentifiersorg .
      optional { ?drug dv:x-ahfs ?xahfs . }
      optional { ?drug dv:x-atc ?xatc . }
      optional { ?drug dv:x-bindingdb ?xbindingdb . }
      optional { ?drug dv:x-cas ?xcas . }
      optional { ?drug dv:x-chebi ?xchebi . }
      optional { ?drug dv:x-chemspider ?xchemspider . }
      optional { ?drug dv:x-dpd ?xdpd . }
      optional { ?drug dv:x-genbank ?xgenbank . }
      optional { ?drug dv:x-gtp ?xgtp . }
      optional { ?drug dv:x-iuphar ?xiuphar . }
      optional { ?drug dv:x-kegg ?xkegg . }
      optional { ?drug dv:x-ndc ?xndc . }
      optional { ?drug dv:x-pdb ?xpdb . }
      optional { ?drug dv:x-pharmgkb ?xpharmgkb . }
      optional { ?drug dv:x-pubchemcompound ?xpubchemcompound . }
      optional { ?drug dv:x-pubchemsubstance ?xpubchemsubstance . }
      optional { ?drug dv:x-uniprot ?xuniprot . }
      optional { ?drug dv:x-wikipedia ?xwikipedia . }
      ?drug void:inDataset ?dataset .
      ?dataset dcat:distribution ?distribution .
      ?distribution dcterms:created ?distributionCreated .
      ?distribution dcterms:source ?distributionSource .
      ?distributionSource pav:retrievedOn ?distributionSourceRetrievedOn .
      ?distributionSource dcterms:publisher ?distributionSourcePublisher .
    }
  }
  bind(iri(concat(str(?drug),"_nanopub")) as ?nanopub)
  bind(iri(concat(str(?nanopub),"head")) as ?headGraph)
  bind(iri(concat(str(?nanopub),"assertion")) as ?assertionGraph)
  bind(iri(concat(str(?nanopub),"provenance")) as ?provenanceGraph)
  bind(iri(concat(str(?nanopub),"publicationInfo")) as ?publicationInfoGraph)
  bind(iri(concat(str(?distributionSource),"#retrieved-on:",str(?distributionSourceRetrievedOn))) as ?distributionRetrievedSource)
  bind(now() as ?now)
}
