prefix np: <http://www.nanopub.org/nschema#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix sio: <http://semanticscience.org/resource/>
prefix has-source: <http://semanticscience.org/resource/SIO_000253>
prefix void: <http://rdfs.org/ns/void#>
prefix pav: <http://purl.org/pav/2.0/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix d: <http://bio2rdf.org/drugbank:>
prefix dv: <http://bio2rdf.org/drugbank_vocabulary:>
prefix dr: <http://bio2rdf.org/drugbank_resource:>

insert {
  graph ?headGraph {
    ?nanopub a np:Nanopublication .
    ?nanopub np:hasAssertion ?assertionGraph .
    ?nanopub np:hasProvenance ?provenanceGraph .
    ?nanopub np:hasPublicationInfo ?publicationInfoGraph .
  }
  graph ?assertionGraph	{
    ?drug a dv:Drug .
    ?drug rdfs:label ?label .
    ?drug rdfs:seeAlso ?seealso .
    ?drug dcterms:title ?title .
    ?drug dcterms:identifier ?identifier .
    ?drug dv:group ?group .
    ?drug dv:manufacturer ?manufacturer .
    ?drug dv:category ?category .
    # TODO: more here
  }
  graph ?provenanceGraph {
    ?assertionGraph has-source: ?dataset .
  }
  graph ?publicationInfoGraph {
    # TODO: author/creator?
    ?nanopub dcterms:created ?now .
    # TODO: license and rights holder?
  }
} where {
  service <http://localhost:8899/sparql> {
    select distinct * {
      ?drug a dv:Drug .
      ?drug rdfs:label ?label .
      ?drug rdfs:seeAlso ?seealso .
      ?drug dcterms:title ?title .
      ?drug dcterms:identifier ?identifier .
      ?drug void:inDataset ?dataset .
      optional { ?drug dv:group ?group . }
      optional { ?drug dv:manufacturer ?manufacturer . }
      optional { ?drug dv:category ?category . }
      # TODO: more here
    }
  }
  bind(iri(concat(str(?drug),"_nanopub")) as ?nanopub)
  bind(iri(concat(str(?nanopub),"head")) as ?headGraph)
  bind(iri(concat(str(?nanopub),"assertion")) as ?assertionGraph)
  bind(iri(concat(str(?nanopub),"provenance")) as ?provenanceGraph)
  bind(iri(concat(str(?nanopub),"publicationInfo")) as ?publicationInfoGraph)
  bind(now() as ?now)
}
