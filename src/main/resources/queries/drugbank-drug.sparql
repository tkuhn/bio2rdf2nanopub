prefix np: <http://www.nanopub.org/nschema#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sio: <http://semanticscience.org/resource/>
prefix has-source: <http://semanticscience.org/resource/SIO_000253>
prefix void: <http://rdfs.org/ns/void#>
prefix pav: <http://purl.org/pav/2.0/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix dv: <http://bio2rdf.org/drugbank_vocabulary:>

insert {
  graph ?headGraph {
    ?nanopub a np:Nanopublication .
    ?nanopub np:hasAssertion ?assertionGraph .
    ?nanopub np:hasProvenance ?provenanceGraph .
    ?nanopub np:hasPublicationInfo ?publicationInfoGraph .
  }
  graph ?assertionGraph	{
    ?drug a dv:Drug .
    ?drug dv:manufacturer ?manufacturer .
    # TODO: more here
  }
  graph ?provenanceGraph {
    ?assertionGraph has-source: ?dataset .
  }
  graph ?publicationInfoGraph {
    ?nanopub pav:authoredBy <http://tw.rpi.edu> ;
    pav:createdBy <http://tw.rpi.edu> ;
    dcterms:created ?now ;
    dcterms:rights <http://opendatacommons.org/licenses/odbl/1.0/> ; #which license to use ?
    dcterms:rightsHolder <http://tw.rpi.edu> .
  }
} where {
  { service <http://cu.drugbank.bio2rdf.org/sparql> {
      select distinct *{
          ?drug a dv:Drug .
          optional { ?drug dv:manufacturer ?manufacturer . }
          # TODO: more here
          ?drug void:inDataset ?dataset .
      }
    }
  }
  bind(iri(concat(str(?drug),"_nanopub")) as ?nanopub)
  bind(iri(concat(str(?nanopub),"head")) as ?headGraph)
  bind(iri(concat(str(?nanopub),"assertion")) as ?assertionGraph)
  bind(iri(concat(str(?nanopub),"provenance")) as ?provenanceGraph)
  bind(iri(concat(str(?nanopub),"publicationInfo")) as ?publicationInfoGraph)
  bind(now() as ?now)
}
