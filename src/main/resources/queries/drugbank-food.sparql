prefix np: <http://www.nanopub.org/nschema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix sio: <http://semanticscience.org/resource/>
prefix has-source: <http://semanticscience.org/resource/SIO_000253>
prefix void: <http://rdfs.org/ns/void#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix d: <http://bio2rdf.org/drugbank:>
prefix dv: <http://bio2rdf.org/drugbank_vocabulary:>
prefix dr: <http://bio2rdf.org/drugbank_resource:>

insert {
  graph ?headGraph {
    ?nanopub a np:Nanopublication .
    ?nanopub np:hasAssertion ?assertionGraph .
    ?nanopub np:hasProvenance ?provenanceGraph .
    ?nanopub np:hasPublicationInfo ?publicationInfoGraph .
  }
  graph ?assertionGraph	{
    ?fi a dv:Food-interaction .
    ?fi rdfs:label ?label .
    ?fi dcterms:title ?title .
    ?fi rdf:value ?value .
    ?fi dcterms:identifier ?identifier .
    ?drug dv:food-interaction ?fi .
  }
  graph ?provenanceGraph {
    ?assertionGraph has-source: ?dataset .
  }
  graph ?publicationInfoGraph {
    # TODO: author/creator?
    ?nanopub dcterms:license <https://creativecommons.org/licenses/by-nc/4.0/> .
  }
} where {
  service <http://localhost:8899/sparql> {
    select distinct * {
      ?fi a dv:Food-interaction .
      ?fi rdfs:label ?label .
      ?fi dcterms:title ?title .
      ?fi rdf:value ?value .
      ?fi dcterms:identifier ?identifier .
      ?fi void:inDataset ?dataset .
      ?drug dv:food-interaction ?fi .
    }
  }
  bind(iri(concat(str(?fi),"_nanopub")) as ?nanopub)
  bind(iri(concat(str(?nanopub),"head")) as ?headGraph)
  bind(iri(concat(str(?nanopub),"assertion")) as ?assertionGraph)
  bind(iri(concat(str(?nanopub),"provenance")) as ?provenanceGraph)
  bind(iri(concat(str(?nanopub),"publicationInfo")) as ?publicationInfoGraph)
  bind(now() as ?now)
}
